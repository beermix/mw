# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>
# Contributor: Mateusz Mikula <mati865@gmail.com>

_realname=binutils
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
pkgver=2.34
pkgrel=2
pkgdesc="A set of programs to assemble and manipulate binary and object files (mingw-w64)"
arch=('any')
url="https://www.gnu.org/software/binutils/"
license=('GPL')
groups=("${MINGW_PACKAGE_PREFIX}-toolchain")
depends=("${MINGW_PACKAGE_PREFIX}-libiconv" "${MINGW_PACKAGE_PREFIX}-zlib")
#checkdepends=('dejagnu' 'bc')
makedepends=("${MINGW_PACKAGE_PREFIX}-libiconv" "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' '!distcc' '!ccache') # 'debug' '!strip')
#install=binutils.install
source=(https://ftp.gnu.org/gnu/binutils/${_realname}-${pkgver}.tar.xz
        0001-enable-gold-on.mingw32.patch
        0002-check-for-unusual-file-harder.patch
        0010-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch
        0020-binutils_2.31_mkdtemp_impl.patch
        0110-binutils-mingw-gnu-print.patch
        0300-default-secure-pe-flags.patch
        0310-reproducible-import-libraries.patch
        binutils-stable-branch.patch
        nodocs.patch
        0015-bfd-install-config.h.patch
        0003-enable-shared-bfd.all.patch
        0003-link-to-libibtl-and-libiberty.mingw.patch
        0004-libiberty-la.mingw.patch
        0005-shared-opcodes.mingw.patch
        0008-fix-libiberty-makefile.mingw.patch
        0009-fix-libiberty-configure.mingw.patch
        0013-dont-link-gas-to-libiberty.mingw.patch
        0014-dont-link-binutils-to-libiberty.mingw.patch
        0015-dont-link-ld-to-libiberty.mingw.patch
        0017-fix-iconv-linking.all.patch
        0200-remove-provide-qualifiers.patch
        0003-MinGW-w64-winpthreads-doesnt-have-pthread_mutexattr_settype.patch
        006_better_file_error.patch
        013_bash_in_ld_testsuite.patch
        130_gold_disable_testsuite_build.patch
        135_bfd_soversion.patch
        binutils-stable-34branch.patch
        branch-version.patch
        binutils-2.29-filename-in-error-messages.patch
        binutils-export-demangle.h.patch)

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -p1 -i "${srcdir}"/nodocs.patch

 #patch -p1 -i "${srcdir}"/binutils-stable-34branch.patch
  patch -p1 -i "${srcdir}"/binutils-stable-branch.patch

 #patch -p1 -i "${srcdir}"/binutils-2.22.52.0.4-no-config-h-check.patch
  patch -p1 -i "${srcdir}"/binutils-2.29-filename-in-error-messages.patch
 #patch -p1 -i "${srcdir}"/binutils-export-demangle.h.patch
 #patch -p1 -i "${srcdir}"/binutils-readelf-other-sym-info.patch

  patch -p1 -i "${srcdir}"/0001-enable-gold-on.mingw32.patch
  patch -p1 -i "${srcdir}"/0002-check-for-unusual-file-harder.patch
  patch -p1 -i "${srcdir}"/0010-bfd-Increase-_bfd_coff_max_nscns-to-65279.patch
  patch -p1 -i "${srcdir}"/0020-binutils_2.31_mkdtemp_impl.patch
  patch -p1 -i "${srcdir}"/0110-binutils-mingw-gnu-print.patch
  patch -p1 -i "${srcdir}"/006_better_file_error.patch

 #patch -p1 -i "${srcdir}"/130_gold_disable_testsuite_build.patch
 #patch -p1 -i "${srcdir}"/135_bfd_soversion.patch
  patch -p1 -i "${srcdir}"/branch-version.patch
  patch -p1 -i "${srcdir}"/0003-MinGW-w64-winpthreads-doesnt-have-pthread_mutexattr_settype.patch
 #patch -p1 -i "${srcdir}"/0015-bfd-install-config.h.patch

  #patch -p2 -i "${srcdir}"/0300-default-secure-pe-flags.patch
  #patch -p2 -i "${srcdir}"/0310-reproducible-import-libraries.patch
}

build() {
  if [ "${CARCH}" = "x86_64" ]; then
    local _conf='--enable-64-bit-bfd'
  else
    local _conf=''
    LDFLAGS+=" -Wl,--large-address-aware"
  fi

  if check_option "debug" "y"; then
    CFLAGS+=" -O0"
  fi

#  export GCC_IGNORE_WERROR=1
#  export AR=gcc-ar
#  export RANLIB=gcc-ranlib
#  export NM=gcc-nm
#  export CFLAGS="$CFLAGS -fcf-protection=full"
#  export FCFLAGS="$CFLAGS -fcf-protection=full"
#  export FFLAGS="$CFLAGS -fcf-protection=full"
#  export CXXFLAGS="$CXXFLAGS -fcf-protection=full"

  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}
  ../${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --disable-werror \
    --enable-lto \
    --with-sysroot=${MINGW_PREFIX} \
    --with-libiconv-prefix=${MINGW_PREFIX} \
    ${_conf} \
    --enable-nls \
    --disable-rpath \
    --disable-multilib \
    --enable-install-libiberty \
    --enable-plugins \
    --enable-gold \
    --disable-shared

  make
}

check() {
  cd ${srcdir}/build-${MINGW_CHOST}

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check || true
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}${MINGW_PREFIX}/{lib,include}/${_realname}
  mkdir -p ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include
  cp ${pkgdir}${MINGW_PREFIX}/include/ansidecl.h ${pkgdir}${MINGW_PREFIX}/${MINGW_CHOST}/include/
  mv ${pkgdir}${MINGW_PREFIX}/include/*.h ${pkgdir}${MINGW_PREFIX}/include/${_realname}
  mv ${pkgdir}${MINGW_PREFIX}/lib/*.a ${pkgdir}${MINGW_PREFIX}/lib/${_realname}

  # Add some useful headers
  #install -m644 ${srcdir}/${_realname}-${pkgver}/include/libiberty.h ${pkgdir}${MINGW_PREFIX}/include
  #install -m644 ${srcdir}/${_realname}-${pkgver}/include/demangle.h ${pkgdir}${MINGW_PREFIX}/include

  find ${pkgdir}${MINGW_PREFIX}/share -type f -iname "opcodes.mo" -o -iname "bfd.mo" | xargs -rtl1 rm
}
