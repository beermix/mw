# Maintainer: Martell Malone <martellmalone@gmail.com>

_realname=getdns
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=7fe308f718240419106c8125dbf4c34c336c2ac8
pkgrel=1
pkgdesc='getdns (mingw-w64)'
arch=('any')
url='https://github.com/getdnsapi/getdns'
license=('GPL2' 'custom')
options=('strip')
depends=("${MINGW_PACKAGE_PREFIX}-libev"
         "${MINGW_PACKAGE_PREFIX}-libevent"
         "${MINGW_PACKAGE_PREFIX}-openssl"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake" "${MINGW_PACKAGE_PREFIX}-gcc")
source=("${_realname}-${pkgver}.tar.gz"::"https://github.com/getdnsapi/getdns/archive/${pkgver}.tar.gz")
noextract=(${_realname}-${pkgver}.tar.gz)

prepare() {
  tar --exclude COPYING -zxf ${_realname}-${pkgver}.tar.gz
  cd ${_realname}-${pkgver}
}

build () {
  [[ -d "${srcdir}"/build-${MINGW_CHOST} ]] && rm -rf "${srcdir}"/build-${MINGW_CHOST}
  mkdir -p "${srcdir}"/build-${MINGW_CHOST} && cd "${srcdir}"/build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_SHARED=0 \
    -DENABLE_STATIC=1 \
    -DBUILD_TESTING=0 \
    -DUSE_LIBIDN2=0 \
    -DUSE_GNUTLS=0 \
    ../${_realname}-${pkgver}

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
}
