# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=grep
pkgver=3.5
pkgrel=1
pkgdesc="A string search utility"
arch=('i686' 'x86_64')
license=('GPL3')
url="https://www.gnu.org/software/grep/"
groups=('base-devel')
depends=('libiconv' 'libintl' 'libpcre' 'sh')
makedepends=('texinfo' 'gettext-devel' 'libiconv-devel' 'pcre-devel')
install=${pkgname}.install
source=(https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig})
sha256sums=('b82ac77707c2ab945520c8404c9fa9f890f7791a62cf2103cf6238acad87a44a'
            'SKIP')
validpgpkeys=('155D3FC500C834486D1EEA677FD9FCCB000BEEEE')  # Jim Meyering

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  #make test cases work
  sed -i "s|case \$perms in drwx--\[-S\]---\*|case \$perms in drwx\*|" tests/init.sh

  autoreconf -vfi
}

build() {
  mkdir -p build-${pkgname}-${pkgver}-${CHOST}
  pushd build-${pkgname}-${pkgver}-${CHOST}
  echo ${CHOST}
  ../${pkgname}-${pkgver}/configure -C \
      --build=${CHOST} \
      --host=${CHOST} \
      --target=${CHOST} \
      --prefix=/usr \
      --without-libiconv-prefix \
      --without-libintl-prefix
  make
  popd
}

check() {
  # grep 3.5: 1 test named 'surrogate-pair' failed
  make -C build-${pkgname}-${pkgver}-${CHOST} check || true
}

package() {
  make -C build-${pkgname}-${pkgver}-${CHOST} DESTDIR=${pkgdir} install
}
