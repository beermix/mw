# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ncurses
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_base_ver=6.0
_date_rev=20190630
pkgver=${_base_ver}
pkgrel=2
pkgdesc="System V Release 4.0 curses emulation library (mingw-w64)"
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-libsystre")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
url="https://www.gnu.org/software/ncurses/"
license=('MIT')
options=('staticlibs' 'strip')
source=("https://invisible-mirror.net/archives/ncurses/ncurses-6.0.tar.gz"
        001-use-libsystre.patch
        100-ncurses-5.6-20080112-urxvt.patch
        101-ncurses-5.6-20080628-kbs.patch
        102-ncurses-5.9-gcc-5.patch
        200-fix_missing_include.patch
        900-terminfo.patch)
sha256sums=('79b54f774ab8f8611d412437a8beb1dd79179861570e96145a97904a1df98d42'
            'eb28949297a4c1eda67da49cfbed1d60f181b83101a0b3715552a4564bd90ff3')

prepare() {
  cd ${_realname}-${_base_ver}
  patch -p1 -i ${srcdir}/001-use-libsystre.patch
  patch -p1 -i ${srcdir}/100-ncurses-5.6-20080112-urxvt.patch
  patch -p1 -i ${srcdir}/101-ncurses-5.6-20080628-kbs.patch
  patch -p1 -i ${srcdir}/102-ncurses-5.9-gcc-5.patch
  patch -p1 -i ${srcdir}/200-fix_missing_include.patch
  patch -p1 -i ${srcdir}/900-terminfo.patch
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  ../${_realname}-${_base_ver}/configure \
    --prefix=${MINGW_PREFIX} \
    --mandir=${MINGW_PREFIX}/share/man \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --without-ada \
    --with-cxx \
    --without-shared \
    --without-pthread \
    --enable-pc-files \
    --disable-rpath \
    --enable-colorfgbg \
    --enable-ext-colors \
    --enable-ext-mouse \
    --disable-symlinks \
    --enable-warnings \
    --enable-assertions \
    --disable-home-terminfo \
    --enable-database \
    --enable-sp-funcs \
    --enable-term-driver \
    --enable-interop \
    --enable-widec \
    --with-build-cppflags=-D_GNU_SOURCE

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
  cp -r ${pkgdir}${MINGW_PREFIX}/include/ncursesw ${pkgdir}${MINGW_PREFIX}/include/ncurses
  cp ${pkgdir}${MINGW_PREFIX}/lib/libncursesw.a ${pkgdir}${MINGW_PREFIX}/lib/libncurses.a
}
