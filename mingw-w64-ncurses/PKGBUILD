# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ncurses
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_base_ver=6.1
_date_rev=20180407
pkgver=${_base_ver}
pkgrel=1
pkgdesc="System V Release 4.0 curses emulation library (mingw-w64)"
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-libsystre")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
url="https://www.gnu.org/software/ncurses/"
license=('MIT')
options=('staticlibs' 'strip')
source=("${_realname}-${pkgver}.tar.gz"::"https://invisible-mirror.net/archives/ncurses/current/${_realname}-${_base_ver}.tgz"
        001-use-libsystre.patch)
sha256sums=('85baae1a02f695cc8a9b966e8dceea457df183ece9229f8e6a20ec3c3d2ee6a9'
            'eb28949297a4c1eda67da49cfbed1d60f181b83101a0b3715552a4564bd90ff3')

prepare() {
  cd ${_realname}-${_base_ver}
  patch -p1 -i ${srcdir}/001-use-libsystre.patch
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir -p build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  CFLAGS+=" -fPIC" \
  ../${_realname}-${_base_ver}/configure \
    --prefix=${MINGW_PREFIX} \
    --without-ada \
    --with-shared \
    --with-cxx-shared \
    --with-normal \
    --without-debug \
    --disable-relink \
    --disable-rpath \
    --with-ticlib \
    --without-termlib \
    --enable-widec \
    --enable-ext-colors \
    --enable-ext-mouse \
    --enable-sp-funcs \
    --with-wrap-prefix=ncwrap_ \
    --enable-sigwinch \
    --disable-term-driver \
    --enable-colorfgbg \
    --enable-tcap-names \
    --disable-termcap \
    --disable-mixed-case \
    --with-pkg-config \
    --enable-pc-files \
    --with-manpage-format=normal \
    --with-manpage-aliases \
    --with-default-terminfo-dir=/mingw64/share/terminfo \
    --enable-echo \
    --mandir=/mingw64/share/man \
    --includedir=/mingw64/include/ncursesw \
    --with-build-cflags=-D_XOPEN_SOURCE_EXTENDED \
    --with-pkg-config-libdir=/mingw64/lib/pkgconfig

  make
  make DESTDIR=${srcdir}/dest install

  noti make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
  cp -r ${pkgdir}${MINGW_PREFIX}/include/ncursesw ${pkgdir}${MINGW_PREFIX}/include/ncurses
  cp ${pkgdir}${MINGW_PREFIX}/lib/libncursesw.a ${pkgdir}${MINGW_PREFIX}/lib/libncurses.a
}
