# Maintainer: Chocobo1

_realname=zstd
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4.4
pkgrel=1
pkgdesc="Zstandard - Fast real-time compression algorithm (mingw-w64)"
arch=('any')
url='https://facebook.github.io/zstd/'
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-meson")
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/facebook/${_realname}/archive/v${pkgver}.tar.gz"
        install-pkgconfig.patch
        zstd-1.4.0-fileio-mingw.patch
        zstd-1.4.0-fix-build-pzstd.patch)
sha256sums=('5eda3502ecc285c3c92ee0cc8cd002234dee39d539b3f692997a0e80de1d33de'
            '9a8864dd11ad6f1eaf1e11ee19adfdbc4e023f20b14b429b8af2b387ffffdef4'
            '210e57fc4bff62e1af1ca8a3da3960de32a91255ba62b057c6bed5b3a7ebb3d5'
            '5095624a5d59054053a257c6b8ec2156b410a44e0e491b259c276c4b7650f499')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  patch -Np1 -i "${srcdir}/install-pkgconfig.patch"
  patch -Np1 -i "${srcdir}/zstd-1.4.0-fileio-mingw.patch"
  patch -Np1 -i "${srcdir}/zstd-1.4.0-fix-build-pzstd.patch"
}

build() {
  [[ -d "${srcdir}"/build-${CARCH} ]] && rm -rf "${srcdir}"/build-${CARCH}
  mkdir -p "${srcdir}"/build-${CARCH} && cd "${srcdir}"/build-${CARCH}

   meson \
    --buildtype=plain \
     -Ddefault_library=both \
     -Ddebug_level=0 \
     -Dbin_programs=true \
     -Dbin_tests=false \
     "../zstd-${pkgver}/build/meson"

   ninja
}


package() {
  cd "${srcdir}"/build-${CARCH}
  DESTDIR="${pkgdir}${MINGW_PREFIX}" ninja install
}
