# Maintainer: Chocobo1

_realname=zstd
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.4.8
pkgrel=1
pkgdesc="Zstandard - Fast real-time compression algorithm (mingw-w64)"
arch=('any')
url='https://github.com/facebook/zstd/releases'
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-meson")
source=(${_realname}-${pkgver}.tar.gz::"https://github.com/facebook/${_realname}/archive/v${pkgver}.tar.gz"
        install-pkgconfig.patch
        zstd-1.4.0-fileio-mingw.patch
        zstd-1.4.0-fix-build-pzstd.patch
        zstd-1.4.5-fix-install-shared-library.patch)

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
  #patch -Np1 -i "${srcdir}/install-pkgconfig.patch"
  patch -Np1 -i "${srcdir}/zstd-1.4.0-fileio-mingw.patch"
  #patch -Np1 -i "${srcdir}/zstd-1.4.0-fix-build-pzstd.patch"
  #patch -Np1 -i "${srcdir}/zstd-1.4.5-fix-install-shared-library.patch"
}

build() {
  msg "Build static version"
  [[ -d "${srcdir}"/build-${CARCH}-static ]] && rm -rf "${srcdir}"/build-${CARCH}-static
  mkdir "${srcdir}"/build-${CARCH}-static && cd "${srcdir}"/build-${CARCH}-static

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  meson \
    --prefix="${MINGW_PREFIX}" \
    --buildtype=plain \
    --default-library=static \
    -Ddebug_level=0 \
    -Dmulti_thread=enabled \
    -Dstatic_runtime=true \
    "../${_realname}-${pkgver}/build/meson"

    nice -n19 ninja -j7

  msg "Build shared version"
  [[ -d "${srcdir}"/build-${CARCH}-shared ]] && rm -rf "${srcdir}"/build-${CARCH}-shared
  mkdir "${srcdir}"/build-${CARCH}-shared && cd "${srcdir}"/build-${CARCH}-shared

  MSYS2_ARG_CONV_EXCL="--prefix=" \
  meson \
    --prefix="${MINGW_PREFIX}" \
    --buildtype=plain \
    -Ddefault_library=shared \
    -Ddebug_level=0 \
    -Dmulti_thread=enabled \
    -Dstatic_runtime=false \
    "../${_realname}-${pkgver}/build/meson"

    nice -n19 ninja -j7
}


package() {
  cd "${srcdir}/build-${CARCH}-static"
  DESTDIR="${pkgdir}" ninja install

  cd "${srcdir}/build-${CARCH}-shared"
  DESTDIR="${pkgdir}" ninja install

  for pcfile in "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig/*.pc; do
    sed -s "s|$(cygpath -m ${MINGW_PREFIX})|${MINGW_PREFIX}|g" -i "${pcfile}"
  done
}
