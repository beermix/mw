# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=libev
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=4.31
pkgrel=1
arch=("any")
url="http://software.schmorp.de/pkg/libev.html"
license=('custom:ISC')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
source=("http://dist.schmorp.de/libev/Attic/${_realname}-${pkgver}.tar.gz")
sha256sums=('2d5526fc8da4f072dd5c73e18fbb1666f5ef8ed78b73bba12e195cfdd810344e')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  libtoolize --force && aclocal -I m4 && autoheader&&  autoconf && automake --add-missing
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST}

  make
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}"${MINGW_PREFIX}/include/event.h
}
