# Maintainer: Alexey Pavlov <alexpux@gmail.com>
# Contributor: Ray Donnelly <mingw.android@gmail.com>

_realname=libtool
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=2.4.6+42+gb88cebd5
pkgrel=7
pkgdesc="A system independent dlopen wrapper for GNU libtool (mingw-w64)"
arch=(x86_64)
url='https://www.gnu.org/software/libtool'
license=(GPL)
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "help2man" "texinfo")
options=('staticlibs' 'strip')
_commit=b88cebd510add4420dd8f5367e3cc6e6e1f267cd
source=(git+https://git.savannah.gnu.org/git/libtool.git#commit=$_commit
        git+https://git.savannah.gnu.org/git/gnulib.git
        gnulib-bootstrap::git+https://github.com/gnulib-modules/bootstrap.git
        https://sources.debian.org/data/main/libt/libtool/2.4.6-11/debian/patches/no_hostname.patch
        0002-cygwin-mingw-Create-UAC-manifest-files.mingw.patch
        0003-Pass-various-runtime-library-flags-to-GCC.mingw.patch
        0005-Fix-seems-to-be-moved.patch
        0006-Fix-strict-ansi-vs-posix.patch
        0007-fix-cr-for-awk-in-configure.all.patch
        0008-tests.patch
        0011-Pick-up-clang_rt-static-archives-compiler-internal-l.patch
        0012-Prefer-response-files-over-linker-scripts-for-mingw-.patch
        0013-Allow-statically-linking-compiler-support-libraries-.patch)

pkgver() {
  cd ${srcdir}/${_realname}
  git describe --tags | sed 's/-/+/g;s/^v//'
}

prepare() {
  cd ${srcdir}/${_realname}

  patch -p1 -i ${srcdir}/0002-cygwin-mingw-Create-UAC-manifest-files.mingw.patch
  patch -p1 -i ${srcdir}/0003-Pass-various-runtime-library-flags-to-GCC.mingw.patch
  patch -p1 -i ${srcdir}/0005-Fix-seems-to-be-moved.patch
  patch -p1 -i ${srcdir}/0006-Fix-strict-ansi-vs-posix.patch
  patch -p1 -i ${srcdir}/0008-tests.patch
  patch -p1 -i ${srcdir}/0011-Pick-up-clang_rt-static-archives-compiler-internal-l.patch
  patch -p1 -i ${srcdir}/0012-Prefer-response-files-over-linker-scripts-for-mingw-.patch
  patch -p1 -i ${srcdir}/0013-Allow-statically-linking-compiler-support-libraries-.patch

  patch -Np1 -i ${srcdir}/no_hostname.patch

  git submodule init
  git config --local submodule.gnulib.url "$srcdir/gnulib"
  git config --local submodule.gl-mod/bootstrap.url "$srcdir/gnulib-bootstrap"
  git submodule update

  ./bootstrap

  patch -p1 -i ${srcdir}/0007-fix-cr-for-awk-in-configure.all.patch
}

build() {
  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p ${srcdir}/build-${MINGW_CHOST} && cd ${srcdir}/build-${MINGW_CHOST}
  ../${_realname}/configure \
      --prefix=${MINGW_PREFIX} \
      --build=${MINGW_CHOST} \
      --host=${MINGW_CHOST} \
      --target=${MINGW_CHOST}

  make -j1
}

check() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make check gl_public_submodule_commit=
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -j1 DESTDIR="${pkgdir}" install
  # Because libtool will have queried native compilers,
  # it'll contain native paths, so sed them back again.
  local MINGW_PREFIX_W=$(cygpath -m "${MINGW_PREFIX}")
  sed -i "s#${MINGW_PREFIX_W}#${MINGW_PREFIX}#g" "${pkgdir}${MINGW_PREFIX}"/bin/libtool

#  rm -rf ${pkgdir}/usr/${_arch}/share/info/dir
}
